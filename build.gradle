buildscript {
    ext.bouncycastleVersion = '1.54'
    ext.jacksonVersion = '2.8.5'
    ext.javapoetVersion = '1.7.0'
    ext.jnr_unixsocketVersion = '0.21'
    ext.okhttpVersion = '3.8.1'
    ext.rxjavaVersion = '2.2.2'
    ext.slf4jVersion = '1.7.25'
    ext.javaWebSocketVersion = '1.3.8'
    ext.picocliVersion = '3.0.0'
    ext.streamsupportVersion = '1.7.0'

    // test dependencies
    ext.equalsverifierVersion = '2.1.7'
    ext.junitVersion = '4.11'
    ext.junitBenchmarkVersion = '0.7.2'
    ext.logbackVersion = '1.2.3'
    ext.mockitoVersion = '1.10.19'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.4"

    // Required for JFrog Artifactory repository
    id 'jacoco'
    id 'maven-publish'

    // Required for Maven Nexus repository
    id 'maven'
    id 'signing'
    id 'idea'
}

apply plugin: 'java'
apply plugin: 'idea'
// should only be applied to the root project, see
// https://github.com/Codearte/gradle-nexus-staging-plugin#multi-project-build
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

description 'web3j base project'

allprojects {

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'checkstyle'

    repositories {
        mavenCentral()
    }

    // We don't want any compiler warnings
    compileJava {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
    compileTestJava {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

    javadoc {
        options.encoding = 'UTF-8'
    }

    jacoco {
        toolVersion = '0.7.7.201606060606' // See http://www.eclemma.org/jacoco/.
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    tasks.withType(Test) {
        reports.html.destination = file("${reporting.baseDir}/${name}")
    }

    checkstyle {
        toolVersion = "7.7"
        configFile = file("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
    }
}

subprojects {
    apply plugin: 'java'

    dependencies {
        testCompile "junit:junit:$junitVersion",
                "org.mockito:mockito-core:$mockitoVersion"
    }
}

configure(subprojects.findAll { it.name != 'integration-tests' }) {
    // Required for Maven Nexus repository
    apply plugin: 'maven'
    apply plugin: 'signing'

    // Required for JFrog Artifactory repository
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task testJar(type: Jar) {
        classifier = 'tests'
        from sourceSets.test.output
    }

    artifacts {
        archives sourcesJar, javadocJar, testJar
    }

    ext {
        ossrhUsername = project.hasProperty('ossrhUsername') ? project.property('ossrhUsername') : System.getenv('OSSRH_USERNAME')
        ossrhPassword = project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : System.getenv('OSSRH_PASSWORD')
        ossrhRepoUrl = project.hasProperty('ossrhRepoUrl') ? project.property('ossrhRepoUrl') : System.getenv('OSSRH_REPO_URL')
        bintrayUser = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        bintrayKey = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
        bintrayGpgPassphrase = project.hasProperty('bintrayGpgPassphrase') ? project.property('bintrayGpgPassphrase') : System.getenv('BINTRAY_GPG_PASSPHRASE')
        isSnapshotVersion = project.version.endsWith("-SNAPSHOT")
        manualRelease = project.hasProperty('manualRelease') ? project.property('manualRelease').toBoolean() : false
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourcesJar {
                    classifier 'sources'
                }

                artifact testJar {
                    classifier 'tests'
                }

                artifact javadocJar {
                    classifier 'javadoc'
                }

                pom {
                    name = project.name
                    description = project.description
                    url = "https://github.com/web3j/web3j"
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                            distribution = "repo"
                        }
                    }
                    developers {
                        developer {
                            id = "conor10"
                            name = "Conor Svensson"
                            email = "conor10@gmail.com"
                        }
                    }
                    scm {
                        connection = "scm:git:https://github.com/web3j/web3j.git"
                        developerConnection = "scm:git://github.com/web3j/web3j.git"
                        url = "https://github.com/web3j/web3j.git"
                    }
                }
            }
        }
    }

    uploadArchives {
        repositories {
            mavenDeployer {

                onlyIf {
                    // Only run if this is a manual release triggered by a user, or an automated release for snapshots.
                    // We don't need to run this for automated releases.
                    isSnapshotVersion && ossrhUsername != '' && ossrhPassword != '' && ossrhRepoUrl != ''
                }

                repository(url: ossrhRepoUrl) {
                    authentication(
                            userName: ossrhUsername,
                            password: ossrhPassword
                    )
                }

                pom.project {
                    name 'web3j'
                    packaging 'jar'
                    description project.description
                    url 'https://web3j.io'

                    scm {
                        connection 'scm:git:https://github.com/web3j/web3j.git'
                        url 'https://github.com/web3j/web3j.git'
                        developerConnection 'scm:git://github.com/web3j/web3j.git'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'conor10'
                            name 'Conor Svensson'
                            email 'conor10@gmail.com'
                        }
                    }
                }
            }
        }
    }

    bintray {
        user = bintrayUser
        key = bintrayKey
        publications = ['mavenJava']
        publish = true
        pkg {
            repo = 'maven'
            name = 'org.web3j'
            desc = project.description
            userOrg = 'web3j'
            licenses = ['Apache-2.0']
            issueTrackerUrl = 'https://github.com/web3j/web3j/issues'
            vcsUrl = 'https://github.com/web3j/web3j.git'
            websiteUrl = 'https://web3j.io'
            publicDownloadNumbers = true

            version {
                gpg {
                    sign = true
                    passphrase = bintrayGpgPassphrase
                }
                mavenCentralSync {
                    sync = true
                    user = ossrhUsername
                    password = ossrhPassword
                    close = '1'
                }
            }
        }
    }

    task release {
        dependsOn 'build'
        dependsOn 'uploadArchives'

        doLast {
            // Validates the user inputs before a release can be performed.
            if (isSnapshotVersion) {
                // Only need OSSRH details.
                logger.lifecycle("About to perform {} snapshot release ...", manualRelease ? "a manual" : "an automated")

                if (!ossrhUsername || !ossrhPassword || !ossrhRepoUrl) {
                    throw new InvalidUserDataException("Required parameters missing:  'ossrhUsername', 'ossrhPassword', 'ossrhRepoUrl'")
                }

                logger.lifecycle(" - ossrhUsername={}", ossrhUsername)
                logger.lifecycle(" - ossrhPassword={}", ossrhPassword ? "provided" : "not_provided")
                logger.lifecycle(" - ossrhRepoUrl={}", ossrhRepoUrl)
            } else {
                // Release => Need OSSRH and bintray.
                if (!ossrhUsername || !ossrhPassword || !ossrhRepoUrl) {
                    throw new InvalidUserDataException("Required parameters missing:  'ossrhUsername', 'ossrhPassword', 'ossrhRepoUrl'")
                }
                if (!bintrayUser || !bintrayKey || !bintrayGpgPassphrase) {
                    throw new InvalidUserDataException("Required parameters missing:  'bintrayUser', 'bintrayKey', 'bintrayGpgPassphrase'")
                }
                logger.lifecycle("About to perform {} full release ...", manualRelease ? "a manual" : "an automated")
                logger.lifecycle(" - ossrhUsername={}", ossrhUsername)
                logger.lifecycle(" - ossrhPassword={}", ossrhPassword ? "provided" : "not_provided")
                logger.lifecycle(" - ossrhRepoUrl={}", ossrhRepoUrl)
                logger.lifecycle(" - bintrayUser={}", bintrayUser)
                logger.lifecycle(" - bintrayKey={}", bintrayKey ? "provided" : "not_provided")
                logger.lifecycle(" - bintrayGpgPassphrase={}", bintrayGpgPassphrase ? "provided" : "not_provided")
            }
        }

        // Snapshots go to nexus, non-snapshots go to bintray.
        if (isSnapshotVersion) {
            dependsOn 'uploadArchives'
            tasks.findByName('uploadArchives').mustRunAfter 'build'
        } else {
            dependsOn 'bintrayUpload'
            tasks.findByName('bintrayUpload').mustRunAfter 'build'
        }
    }

    task jacocoRootTestReport(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
        dependsOn = subprojects.test
        additionalSourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs)
        sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
        classDirectories = files(subprojects.sourceSets.main.output)
        executionData = files(subprojects.jacocoTestReport.executionData)
        reports {
            xml.enabled = true
        }

        onlyIf = {
            true
        }

        doFirst {
            executionData = files(executionData.findAll {
                it.exists()
            })
        }

        afterEvaluate {
            classDirectories = files(classDirectories.files.collect {
                fileTree(dir: it,
                        exclude: [
                                'org/web3j/abi/datatypes/generated/**',
                                'org/web3j/tuples/generated/**',
                                'org/web3j/ens/contracts/generated/**'
                        ])
            })
        }
    }
}

// Makes a submodule to be compiled against a reduced JDK, currently an Android JDK.
ext.useReducedJdk = { Project project ->
    // Use Android JDK instead of normal JDK in order to limit language APIs allowed in the project
    project.tasks.withType(JavaCompile) { Task task ->
        // As tests are run on a local Java VM they don't have to be compatible with Android JDK.
        if (task.name.contains("compileTest")) return
        options.bootstrapClasspath = files(
                // Contains lambda-related classes such as java.lang.invoke.LambdaMetafactory.
                // Copied from [android-sdk]/build-tools/27.0.3
                "$rootDir/compat/libs/core-lambda-stubs.jar",
                // Android JDK. Copied from [android-sdk]/platforms/android-15. All Android specific
                // packages were stripped. The only kept packages are:
                // java.*, javax.*, org.w3c.* and org.xml.*
                "$rootDir/compat/libs/android.jar")
    }
}
