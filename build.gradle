plugins {
    id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

group 'org.web3j'
version '0.1'

sourceCompatibility = 1.8

// We don't want any compiler warnings
compileJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}
compileTestJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

repositories {
    mavenCentral()
}

// See https://www.petrikainulainen.net/programming/gradle/getting-started-with-gradle-integration-testing/
// exclude with: gradle clean build -x integrationTest
sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile 'org.apache.httpcomponents:httpclient:4.5.2',
            'com.fasterxml.jackson.core:jackson-databind:2.8.1'
    testCompile 'junit:junit:4.11',
            'org.mockito:mockito-core:1.10.19'
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }  // always run integration tests regardless of if up to date

    // view status of integration tests
    testLogging {
        events "passed", "skipped", "failed"
    }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'maven'
        name = 'org.web3j'
        desc = 'web3j is a lightweight Java library for integration with Ethereum clients'
        userOrg = 'conor10'
        licenses = ['MIT']
        issueTrackerUrl = 'https://github.com/conor10/web3j/issues'
        vcsUrl = 'https://github.com/conor10/web3j.git'
        websiteUrl = 'http://web3j.org'
        publicDownloadNumbers = true
    }
}
