apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: "de.marcphilipp.nexus-publish"

task javadocsJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task testsJar(type: Jar) {
    archiveClassifier = 'tests'
    from sourceSets.test.output
}

javadoc { options.encoding = 'UTF-8' }

artifacts { archives sourcesJar, javadocsJar, testsJar }

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocsJar
            artifact testsJar

            version = '4.8.5'

            pom {
                name = project.name
                description.set(project.provider({ project.description }))
                url = 'https://web3j.io'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                scm {
                    def slug = rootProject.name
                    if (project.hasProperty('rootProjectOverride')) {
                        slug = project.rootProjectOverride
                    }

                    url = "https://github.com/web3j/$slug"
                    connection = "scm:https://github.com/web3j/${slug}.git"
                    developerConnection = "scm:git://github.com/web3j/${slug}.git"
                }

                developers {
                    developer {
                        id = 'conor10'
                        name = 'Conor Svensson'
                        email = 'conor10@gmail.com'
                    }
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv('OSSRH_USERNAME')
            password = System.getenv('OSSRH_PASSWORD')
        }
    }
}


nexusStaging {
    username System.getenv('OSSRH_USERNAME')
    password System.getenv('OSSRH_PASSWORD')
    packageGroup = rootProject.group
}


signing {
    sign publishing.publications.maven
    def signingKey = new File("$rootDir/web3j.asc")
    if (signingKey.exists()) {
        useInMemoryPgpKeys(signingKey.getText('UTF-8'), System.getenv('GPG_PASSPHRASE'))
    }
}
